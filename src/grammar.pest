WHITESPACE = _{ (" " | "\t" | "\r" | "\n" | COMMENT)+ }
COMMENT    = _{ LINE_COMMENT | BLOCK_COMMENT }
LINE_COMMENT  = _{ "//" ~ (!"\n" ~ ANY)* }
BLOCK_COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

program   = { SOI ~ statement* ~ EOI }

statement = _{
      block
    | var_decl
    | func_decl
    | extern_block
    | extern_decl
    | struct_decl
    | enum_decl
    | trait_decl
    | impl_decl
    | return_stmt
    | expr_stmt
}

extern_block = { "extern" ~ string? ~ "{" ~ extern_item* ~ "}" }
extern_decl  = { "extern" ~ string? ~ extern_item }
extern_item  = { "fun" ~ identifier ~ "(" ~ (func_param ~ ("," ~ func_param)*)? ~ ")" ~ ("->" ~ type_expr)? ~ ";" }

var_decl  = { ("var" | "let") ~ identifier ~ (":" ~ type_expr)? ~ ("=" ~ value_expr)? ~ ";" }
type_expr = { identifier }
value_expr = { expr }

func_param = { identifier ~ ":" ~ type_expr }

func_decl = { "fun" ~ identifier ~ "(" ~ (func_param ~ ("," ~ func_param)*)? ~ ")" ~ ("->" ~ type_expr)? ~ block }

struct_decl   = { "struct" ~ identifier ~ "{" ~ (struct_field ~ ("," ~ struct_field)*)? ~ ","? ~ "}" }
struct_field  = { identifier ~ ":" ~ type_expr }

enum_decl     = { "enum" ~ identifier ~ "{" ~ (enum_variant ~ ("," ~ enum_variant)*)? ~ ","? ~ "}" }
enum_variant  = { identifier ~ (enum_tuple | enum_struct)? }
enum_tuple    = { "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }
enum_struct   = { "{" ~ (struct_field ~ ("," ~ struct_field)*)? ~ ","? ~ "}" }

trait_decl = { "trait" ~ identifier ~ "{" ~ trait_item* ~ "}" }
trait_item = { "fun" ~ identifier ~ "(" ~ (func_param ~ ("," ~ func_param)*)? ~ ")" ~ ("->" ~ type_expr)? ~ ";" }

impl_decl = { "impl" ~ identifier ~ ("for" ~ type_expr)? ~ "{" ~ func_decl* ~ "}" }

return_stmt = {"return" ~ expr ~ ";"}

block     = { "{" ~ statement* ~ expr? ~ "}" }

expr_stmt = { expr ~ ";" }

expr = _{ binary_expr }

binary_expr  = { postfix_expr ~ (op ~ postfix_expr)* }

postfix_expr = { primary_expr ~ (call_suffix | struct_inst | member_access)* }
member_access = { "." ~ identifier }
struct_inst = { "{" ~ (struct_inst_field ~ ("," ~ struct_inst_field)*)? ~ ","? ~ "}" }
struct_inst_field = { identifier ~ ":" ~ expr }

call_suffix = { "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }

primary_expr = _{
      block
    | literal
    | identifier
    | "(" ~ expr ~ ")"
}

literal = _{ float | number | string | bool }

bool   = { "true" | "false" }

float  = @{ number ~ "." ~ number }

number = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }

string = { "\"" ~ (escape | (!"\"" ~ ANY))* ~ "\"" }
escape = { "\\" ~ ( "\"" | "\\" | "n" | "r" | "t" | "0" ) }

identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

op = @{ op_char+ }
op_char = { "+" | "-" | "*" | "/" | "!" | "@" | "%" | "^" | "&" | "=" | "~" | "<" | ">" }
