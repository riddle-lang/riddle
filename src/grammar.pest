WHITESPACE = _{ (" " | "\t" | "\r" | "\n" | COMMENT)+ }
COMMENT    = _{ LINE_COMMENT | BLOCK_COMMENT }
LINE_COMMENT  = _{ "//" ~ (!"\n" ~ ANY)* }
BLOCK_COMMENT = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

program   = { SOI ~ statement* ~ EOI }

statement = _{
      if_stmt
    | block
    | var_decl
    | func_decl
    | extern_block
    | extern_decl
    | struct_decl
    | enum_decl
    | trait_decl
    | impl_decl
    | return_stmt
    | expr_stmt
}

extern_block = { "extern" ~ string? ~ "{" ~ extern_item* ~ "}" }
extern_decl  = { "extern" ~ string? ~ extern_item }
extern_item  = { "fun" ~ identifier ~ "(" ~ (func_param ~ ("," ~ func_param)*)? ~ ("," ~ variadic_marker)? ~ ")" ~ ("->" ~ type_expr)? ~ ";" }
variadic_marker = { "..." }

var_decl  = { ("var" | "let") ~ identifier ~ (":" ~ type_expr)? ~ ("=" ~ value_expr)? ~ ";" }
type_expr = { array_type | pointer_type | path_type }
array_type = { "[" ~ type_expr ~ ";" ~ number ~ "]" }
pointer_type = { "*" ~ type_expr }
path_type = { path ~ generic_args? }
generic_args = { "<" ~ type_expr ~ ("," ~ type_expr)* ~ ">" }
generic_params = { "<" ~ identifier ~ ("," ~ identifier)* ~ ">" }
value_expr = { expr }

func_param = { identifier ~ ":" ~ type_expr }

func_decl = { "fun" ~ identifier ~ generic_params? ~ "(" ~ (func_param ~ ("," ~ func_param)*)? ~ ")" ~ ("->" ~ type_expr)? ~ block }

struct_decl   = { "struct" ~ identifier ~ generic_params? ~ "{" ~ (struct_field ~ ("," ~ struct_field)*)? ~ ","? ~ "}" }
struct_field  = { identifier ~ ":" ~ type_expr }

enum_decl     = { "enum" ~ identifier ~ generic_params? ~ "{" ~ (enum_variant ~ ("," ~ enum_variant)*)? ~ ","? ~ "}" }
enum_variant  = { identifier ~ (enum_tuple | enum_struct)? }
enum_tuple    = { "(" ~ (type_expr ~ ("," ~ type_expr)*)? ~ ")" }
enum_struct   = { "{" ~ (struct_field ~ ("," ~ struct_field)*)? ~ ","? ~ "}" }

trait_decl = { "trait" ~ identifier ~ generic_params? ~ "{" ~ trait_item* ~ "}" }
trait_item = { "fun" ~ identifier ~ generic_params? ~ "(" ~ (func_param ~ ("," ~ func_param)*)? ~ ")" ~ ("->" ~ type_expr)? ~ ";" }

if_stmt = { "if" ~ "(" ~ expr ~ ")" ~ block ~ ("else" ~ (if_stmt | block))? }
impl_decl = { "impl" ~ generic_params? ~ type_expr ~ ("for" ~ type_expr)? ~ "{" ~ func_decl* ~ "}" }

return_stmt = {"return" ~ expr ~ ";"}

block     = { "{" ~ statement* ~ expr? ~ "}" }

expr_stmt = { expr ~ ";" }

expr = _{ binary_expr }

binary_expr  = _{ assign_expr }
assign_expr  = { compare_expr ~ (assign_op ~ assign_expr)? }
assign_op    = { "=" }
compare_expr = { add_expr ~ (compare_op ~ add_expr)* }
compare_op   = { "==" | "!=" | "<=" | ">=" | "<" | ">" }
add_expr     = { mul_expr ~ (add_op ~ mul_expr)* }
add_op       = { "+" | "-" }
mul_expr     = { unary_expr ~ (mul_op ~ unary_expr)* }
mul_op       = { "*" | "/" }

unary_expr   = { (unary_op)* ~ cast_expr }
unary_op     = { "*" | "&" | "-" | "!" }

cast_expr = { postfix_expr ~ ("as" ~ type_expr)* }

postfix_expr = { primary_expr ~ (call_suffix | member_access | index_access | struct_inst_suffix)* }
member_access = { "." ~ identifier }
index_access = { "[" ~ expr ~ "]" }
struct_inst_suffix = { "{" ~ (struct_inst_field ~ ("," ~ struct_inst_field)*)? ~ ","? ~ "}" }
struct_inst_field = { identifier ~ ":" ~ expr }

call_suffix = { "(" ~ (expr ~ ("," ~ expr)*)? ~ ")" }

primary_expr = _{
      block
    | list_literal
    | literal
    | path
    | "(" ~ expr ~ ")"
}
list_literal = { "[" ~ (expr ~ ("," ~ expr)*)? ~ ","? ~ "]" }

literal = _{ cstr | cint | float | number | string | bool }

cstr = { "cstr" ~ string }
cint = { "cint" ~ number }

bool   = { "true" | "false" }

float  = @{ number ~ "." ~ number }

number = @{ "0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT* }

string = { "\"" ~ (escape | (!"\"" ~ ANY))* ~ "\"" }
escape = { "\\" ~ ( "\"" | "\\" | "n" | "r" | "t" | "0" ) }

identifier = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")* }

path = { identifier ~ ("::" ~ identifier)* }

op = @{ "+" | "-" | "*" | "/" | "==" | "!=" | "<=" | ">=" | "<" | ">" | "=" }
